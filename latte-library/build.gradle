def VERSION_FILE = file('../latte-build/src/main/resources/version')
BufferedReader br = new BufferedReader(new FileReader(VERSION_FILE))
def VERSION = br.readLine().trim()
br.close()

def ACTION = System.getenv("BUILD_ACTION")

group 'org.latte-lang'
version VERSION

buildscript {
    def VERSION_FILE_ = file('../latte-build/src/main/resources/version')
    BufferedReader br_ = new BufferedReader(new FileReader(VERSION_FILE_))
    def VERSION_ = br_.readLine().trim()
    br_.close()

    repositories {
        mavenLocal()
    }
    dependencies {
        classpath 'org.latte-lang:latte-gradle-plugin:' + VERSION_
        classpath 'org.latte-lang:latte-class-recorder:' + VERSION_
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'latte'
if ('deploy' == ACTION) {
    apply plugin: 'signing'
}
apply plugin: 'org.latte-lang.class-recorder'
apply plugin: LatteBuild

sourceSets {
    main {
        java
        latte.srcDirs = ['src/main/latte/async', 'src/main/latte/dsl']
        resources
    }
    test {
        java
        latte
        resources
    }
}

recordConfig {
    directory = 'classes/main'
    file = 'lib-classes.txt'
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 1.6
targetCompatibility = 1.6
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.latte-lang', name: 'latte-compiler', version: VERSION
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            def deploy_user = System.getenv('DEPLOY_USER')
            def deploy_pass = System.getenv('DEPLOY_PASS')

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: deploy_user, password: deploy_pass)
            }

            pom.project {
                name 'latte-library'
                packaging 'jar'
                description 'The latte-lang library project, which contains some useful libraries.'
                url 'http://latte-lang.org'
                scm {
                    url 'https://github.com/wkgcass/Latte-lang'
                    connection 'scm:git:https://github.com/wkgcass/Latte-lang.git'
                    developerConnection 'scm:git:https://github.com/wkgcass/Latte-lang.git'
                }
                licenses {
                    license {
                        name 'MIT LICENSE'
                        url 'https://github.com/wkgcass/Latte-lang/blob/master/LICENSE/'
                    }
                }
                developers {
                    developer {
                        id 'wkgcass'
                        email 'wkgcass@hotmail.com'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

javadoc {
    options {
        encoding 'UTF-8'
        charSet 'UTF-8'
    }
}

if ('deploy' == ACTION) {
    signing {
        sign configurations.archives
    }
}

class LatteBuild implements Plugin<Project> {
    @Override
    void apply(Project project) {
        def latteBuild = project.task('latteBuild')
        project.tasks['install'].dependsOn project.tasks['test']
        latteBuild.dependsOn project.tasks['install']

        def latteTest = project.task('latteTest')
        latteTest.dependsOn project.tasks['test']

        def latteDeploy = project.task('latteDeploy')
        latteDeploy.dependsOn project.tasks['uploadArchives']
    }
}
